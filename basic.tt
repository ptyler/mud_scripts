#class {basic} {kill}
#class {basic} {open}


/*
 * Basics relative to all characters
 ******************************************************************************/

#var {mounted} {0}
#var {inbattle} {0}
#var {wasjustinbattle} {0}
#var {sitting} {0}
#var {resting} {0}
#var {sleeping} {0}
#var {assisting} {0}
#var {container} {pack}
#var {food} {food}
#var {autoloot} {0}
#var {mounted} {0}
#var {ineedtorest} {0}
#var {grouped} {0}

/*
 * default actions?
 *****************************************************************************/

#split

#act {^You raise a level!} {score}
#act {^You are hungry.} { get $food $container; eat $food }
#act {^You are thirsty.} { drink water }

#act {^There were %1 coins.}
{
  #if { $grouped }
  {
    #split %1
  }
}

/*
 * the state of the char!  I know some are almost always needed and some are
 * likely to never be needed except with a bot or some excessive automation.
 * Better to have and not need than to need and not have. ;)
 *****************************************************************************/

#act {^You have %1(%2) hit and %3(%4) movement points.}
{
  #var hp {%1};
  #var maxhp {%2};
  #var mp {%3};
  #var maxmp {%4};
  #math {resthp} {$maxhp * 0.3333}
}

#act {You have scored %1 exp, and have %2(%3) steel coins.}
{
  #var earned_xp {%1};
  #var coins {%2};
  #var bankc {%3}
}

#act {^%1H %2V %3X %5}
{
  #var hp {%1};
  #var mp {%2};
  #var earned_xp {%3};
  #if {$hp <= $resthp}
  {
    #var ineedtorest 1
  };
  #else
  {
    #var ineedtorest 0
  }

}

#act {^You {miss|bludgeon|slash|pierce|bruise|barely|hit} %1 %2}
{
  #if {$inbattle==0 && "%1" != "feel"}
  {
    #var inbattle 1
  }
}

#act {^You now ride a %1} {#var mounted 1}
#act {^You swing into the saddle of this great steed.} {#var mounted 1}
#act {^You throw a leg over the beast, and slide off its side.} {#var mounted 0}
#act {^You no longer ride %1} {#var mounted 0}
#act {^You sit down and rest your tired bones.} {#var resting 1}
#act {^You stand up.}
{
  #var resting 0;
  #var sitting 0;
}

/*
 * since the on_death action can be used in so many places in different contexts,
 * might be a good idea to wrap this in an alias to turn on default_rip or 
 * something of that nature so that once a class is killed, we can return to
 * where we started.
 *****************************************************************************/
#act {^%1 is dead! R.I.P.}
{
  #var inbattle 0;
  #var wasjustinbattle 1;
  exa corpse;
  #if {$autoloot == 1}
  {
    get all.coins corpse;
  }
}

/*
 * These trigger synctick to help, at least a little, to stay in sync with the
 * MUD's internal tick.  These should be class independent triggers.
 *****************************************************************************/

#act {^There is lightning in the sky.} {synctick}

#alias {synctick}
{
  #unticker {ticky};
  #ticker {ticky}
  {
    #showme #TICK!!;
    #delay {50} {#showme #10 SECONDS UNTIL TICK!!}
  } {60}
}


#tick {idle} {#cr} {300}

#high {^%1 tells your group '%2} {<049>}
#high {^You tell %1 '%2} {<039>}
#high {^#10 SECONDS %1} {<039>}

#alias {setfood}  {#var food {%1}}
#alias {setbag}   {#var container {%1}}
#alias {rl}       {#read basic.tt}
#alias {autoloot} {#var autoloot {%1}; #showme {autoloot set to: %1}}
#alias {load.mapping}   {#read mapping.tt}
#alias {load.cleric}    {#read guilds/cleric.tt}

#alias {basic.help}
{
  #showme {<039>,--------------------------------------,};
  #showme {<039>| Chip & Chap Basic MUD Module         |};
  #showme {<039>'--------------------------------------'};
  #showme {<039>,--------------------------------------,};
  #showme {<039>| You set the following:               |};
  #showme {<039>|   setbag  <bag name>                 |};
  #showme {<039>|   setfood <food name>                |};
  #showme {<039>|                                      |};
  #showme {<039>| Optionally:                          |};
  #showme {<039>|    basic.help                        |};
  #showme {<039>|    load.mapping                      |};
  #showme {<039>|    load.{cleric|scout|...etc}        |};
  #showme {<039>'--------------------------------------'<099>};
}


/* need at least 1 score output to populate variables */
score
#delay 1 basic.help

#class {basic} {close}










