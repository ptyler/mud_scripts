#class {paladin} kill
#class {paladin} open

#act {^You are sent sprawling %1} { stand }
#act {^You fumble and drop %1} { get silver.sword; wield silver.sword }
#act {^Your studies complete, you close %1} { synctick }
#act {^Your hands tingle as a feeling of goodness fills you.} {}

/*
 * The actual queue processing is done in doqueue.tt, so that the code doesn't
 * have to be repeated in every guild that would need it.
 *****************************************************************************/

#act {^The gods will grant you audience through prayer again,%1} 
{ 
  queue_ability {beseech paladine}; 
}

#act {^The radiance surrounding you fades away.} 
{ 
  queue_spell {'aura of glory'};
}

#act {^You feel less powerful.} 
{ 
  queue_spell {'holy arms'};
}

#act {^You feel less righteous.} 
{ 
  queue_spell {'bless' me};
}

#act {^Your eyes sting for a moment.}
{
  queue_spell {'holy vision'}
}

/* 
 * eating/drinking handled as casts. 
 *
 * - since you continue to receive hungry/thirsty, a simple action would keep
 *   adding spells to the queue until you're in a state to perform them. 
 *
 * - still dancing around the state && act vs. act... ;)
 *****************************************************************************/
#var {iamthirsty} {0}
#var {iamhungry}  {0}

#unact {^You are hungry.}
#act {^You are thirsty.}
{
  #if {!$iamthirsty}
  {
    #var iamthirsty 1;
    queue_spell {'summon elixir'};
    queue_ability {drink elixir}
  }
}

#act {^You do not feel thirsty.}
{
  #var iamthirsty 0;
  #var iamhungry  0
}

/*****************************************************************************/

/* spell/ability processing handled in one place */

#read guilds/doqueue.tt

/* load message */
#delay 1 system_message {LOADED} {Paladin module} {Type paladin.help for help}

#class {paladin} close
